#version 450

#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require

#define FLOAT_TYPE float

#define QK_40_80 32
#define N_THREADS 16

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BlockQ40 {
    float16_t d;
    uint8_t qs[16];
};

struct BlockQ80 {
    float16_t d;
    int8_t qs[32];
};

layout(binding = 0) readonly buffer inputBuffer { BlockQ80 inpt[]; };
layout(binding = 1) readonly buffer weightsBuffer { BlockQ40 weights[]; };
layout(binding = 2) writeonly buffer outputBuffer { float outp[]; };
layout(binding = 3) readonly uniform metadataBuffer { uint n; };

shared float temp[N_THREADS];

void main() {
    const uint threadIndex = uint(gl_LocalInvocationID.x);
    const uint d = uint(gl_GlobalInvocationID.y);
    const uint nb = n / QK_40_80;

    const uint slice = nb / N_THREADS;
    const uint rest = nb % N_THREADS;
    const uint start = threadIndex * slice + (threadIndex < rest ? threadIndex : rest);
    const uint end = start + slice + (threadIndex < rest ? 1 : 0);

    const uint wOffset = d * nb;

    float16_t s = float16_t(0.0);
    for (uint i = start; i < end; i++) {
        float16_t s0 = float16_t(0.0);
        [[unroll]] for (uint j = 0; j < QK_40_80 / 2; j++) {
            float16_t i0 = float16_t(inpt[i].qs[j]);
            float16_t i1 = float16_t(inpt[i].qs[j + QK_40_80 / 2]);

            uint8_t wq = weights[wOffset + i].qs[j];
            float16_t w0 = float16_t(wq & 0xF) - float16_t(8.0f);
            float16_t w1 = float16_t(wq >>  4) - float16_t(8.0f);

            s0 += i0 * w0 + i1 * w1;
        }
        s += s0 * inpt[i].d * weights[wOffset + i].d;
    }
    temp[threadIndex] = float(s);

    barrier();

    [[unroll]] for (uint i = N_THREADS / 2; i > 0; i >>= 1) {
        if (threadIndex < i)
            temp[threadIndex] += temp[threadIndex + i];
        barrier();
    }

    if (threadIndex == 0) {
        outp[d] = temp[0];
    }
}
